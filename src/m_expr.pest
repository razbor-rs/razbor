mexpr = { SOI ~ seq ~ EOI }
expr = _{ m | list | name | number | string }
seq = _{ (expr ~ ("," ~ expr)*)? ~ ","? }
m = { name ~ "[" ~ seq ~ "]" }
list = { "[" ~ seq ~ "]" }

name = {
    (XID_START ~ ("." | XID_CONTINUE)*)
    | ("_" ~ ("." | XID_CONTINUE)*)
}
number = _{ hexdecimal | decimal }
decimal = { ASCII_DIGIT+ }
hexdecimal = { "0x" ~ ASCII_HEX_DIGIT+ }
string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
}

WHITESPACE = _{ " " | NEWLINE | "\t" }
COMMENT = _{ "//" ~ (!NEWLINE ~ ANY)* ~ (NEWLINE | EOI) }
