mexpr = { SOI ~ seq ~ EOI }
expr = _{ m | list | name | number | string }
seq = _{ (expr ~ ("," ~ expr)*)? ~ ","? }
m = { name ~ "[" ~ seq ~ "]" }
list = { "[" ~ seq ~ "]" }

name = {
    (XID_START ~ ("." | XID_CONTINUE)*)
    | ("_" ~ ("." | XID_CONTINUE)*)
}
number = _{ octal | hexdecimal | decimal }
decimal = {
    ASCII_NONZERO_DIGIT ~ ASCII_DIGIT+ // a number starting with a non-zero digit
    | ASCII_DIGIT                      // a number which is a non-zero digit itself
}
octal = { "0" ~ ASCII_OCT_DIGIT+ }
hexdecimal = { "0x" ~ ASCII_HEX_DIGIT+ }
string = ${ "\"" ~ inner ~ "\"" }
inner = @{ (!"\"" ~ ANY)* }

WHITESPACE = _{ " " | NEWLINE | "\t" }
COMMENT = _{ "//" ~ (!NEWLINE ~ ANY)* ~ (NEWLINE | EOI) }
